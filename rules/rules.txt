ModuleNode = ModuleUnionNode+ .
ModuleUnionNode = FnNode | ConstNode .
FnNode = "(" "def" SymbolNode LambdaNode+ ")" .
SymbolNode = <symbol> .
LambdaNode = "(" "fn" ("[" InputNode+ "]")? "<" DatatypeUnionNode ">" ExpressionUnionNode ")" .
InputNode = SymbolNode ":" DatatypeUnionNode .
DatatypeUnionNode = PrimitiveNode .
PrimitiveNode = ("int" | "float" | "string" | "bool" | "unit") .
ExpressionUnionNode = OperationNode | ListNode | SymbolNode | IntegerNode | FloatNode | BoolNode | StringNode | BlockNode | CondNode | CaseNode | LambdaNode | CallNode .
OperationNode = "(" ("+" | "-" | "*" | "/" | "<" | "=" | "<>" | ">" | "<=" | ">=" | "|" | "&" | "++") ExpressionUnionNode ExpressionUnionNode+ ")" .
ListNode = "list" "<" DatatypeUnionNode ">" "{" ExpressionUnionNode* "}" .
IntegerNode = <integer> .
FloatNode = <float> .
BoolNode = <boolean> .
StringNode = <stringstart> <string> <stringend> .
BlockNode = "{" BlockUnionNode+ "}" .
BlockUnionNode = OperationNode | UnitNode | SymbolNode | CondNode | DefNode | VarNode | SetNode | CallNode .
UnitNode = "()" .
CondNode = "(" "cond" CaseNode+ ExpressionUnionNode ")" .
CaseNode = "(" "case" ExpressionUnionNode ExpressionUnionNode ")" .
DefNode = "(" "def" SymbolNode ExpressionUnionNode ")" .
VarNode = "(" "var" SymbolNode "=" ExpressionUnionNode ")" .
SetNode = "(" "set" SymbolNode "=" ExpressionUnionNode ")" .
CallNode = "(" SymbolNode ExpressionUnionNode* ")" .
ConstNode = "(" "def" SymbolNode ConstantUnionNode ")" .
ConstantUnionNode = IntegerNode | FloatNode | BoolNode | StringNode .